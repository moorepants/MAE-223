% JASON MOORE -- MAY 16, 2006
AUTOZ ON
OVERWRITE ON
% DECLARE THE INERTIAL REFERENCE FRAME
NEWTONIAN N
% DECLARE THREE INTERMEDIATE FRAMES
FRAMES A,B,E
% DECLARE FOUR BODIES: REAR WHEEL,FRAME,FORK,FRONT WHEEL
BODIES C,D,F,G
% DECLARE FOUR POINTS: FRONT CONTACT POINT,REAR CONTACT POINT (ONE ON GROUND AND ONE ON WHEEL)
POINTS NC,CN,NG,GN
% DECLARE THE FRAME DIMENSIONS
CONSTANTS RF,RR,L{7},THETA
% DECLARE THE GENERALIZED COORDINATES
VARIABLES Q{5}',Q7',Q8',PITCH
MOTIONVARIABLES U{5}',U7',U8'

% FRAME YAW
SIMPROT(N,A,3,Q3)
% FRAME ROLL
SIMPROT(A,B,-1,Q4)
% REAR WHEEL ROTATION
SIMPROT(B,C,2,Q5)
% FRAME PITCH
PITCH=(L2*COS(THETA)*COS(Q4)+L3*(SIN(Q4)*SIN(Q7)-SIN(THETA)*COS(Q4)*COS(&
        Q7))-RR*COS(Q4)-RF*(-1+(SIN(Q4)*COS(Q7)+SIN(THETA)*SIN(Q7)*COS(Q4))^2)/&
        (1-(SIN(Q4)*COS(Q7)+SIN(THETA)*SIN(Q7)*COS(Q4))^2)^0.5)/(COS(Q4)*(L1+&
        L2*SIN(THETA)+L3*COS(THETA)*COS(Q7)+2*RF*COS(THETA)*SIN(Q7)*(SIN(Q4)*&
        COS(Q7)+SIN(THETA)*SIN(Q7)*COS(Q4))/(1-(SIN(Q4)*COS(Q7)+SIN(THETA)*SIN(&
        Q7)*COS(Q4))^2)^0.5+RF*COS(THETA)*SIN(Q7)*(SIN(Q4)*COS(Q7)+SIN(THETA)*&
        SIN(Q7)*COS(Q4))*(-1+(SIN(Q4)*COS(Q7)+SIN(THETA)*SIN(Q7)*COS(Q4))^2)/(1&
        -(SIN(Q4)*COS(Q7)+SIN(THETA)*SIN(Q7)*COS(Q4))^2)^1.5))
SIMPROT(B,D,-2,PITCH)
% HEAD TUBE ANGLE
SIMPROT(D,E,-2,THETA)
% STEERING ANGLE
SIMPROT(E,F,3,Q7)
% FRONT WHEEL ROTATION
SIMPROT(F,G,2,Q8)

ANGVEL(N,A)
ANGVEL(A,B)
ANGVEL(B,C)
ANGVEL(B,D)
ANGVEL(D,E)
ANGVEL(E,F)
ANGVEL(F,G)

% CALCULATE THE UNIT VECTOR ALONG THE LINE FROM GO TO GN
RFHAT>=UNITVEC(DOT(F2>,N3>)*F2>-N3>)
% BUILD THE POSITION VECTOR FROM THE INERTIAL ORIGIN TO NC
P_NO_NC>=Q1*N1>+Q2*N2>
% BUILD THE POSITION VECTOR FROM NC TO CN
P_NC_CN>=0>
% BUILD THE POSITION VECTOR FROM CN TO CO
P_CN_CO>=RR*B3>
% BUILD THE POSITION VECTOR FROM CO TO GO
P_CO_GO>=L1*D1>-L2*F3>+L3*F1>
% BUILD THE POSITION VECTOR FROM GO TO GN
P_GO_GN>=RF*RFHAT>
% BUILD THE POSITION VECTOR FROM GN TO NG
P_GN_NG>=0>
% BUILD THE POSITION VECTOR FROM CO TO DO
P_CO_DO>=L4*D1>+L5*D3>
% BUILD THE POSITION VECTOR FROM GO TO FO
P_GO_FO>=L6*F1>+L7*F3>

% CALCULATE THE VELOCITY OF POINT CO
V_CO_N>=EXPLICIT(DT(P_NO_CO>,N))
% CALCULATE THE VELOCITY OF POINT CN
V2PTS(N,C,CO,CN)
% CALCULATE THE VELOCITY OF POINT GO
V_GO_N>=EXPLICIT(DT(P_NO_GO>,N))
% CALCULATE THE VELOCITY OF POINT GN
V2PTS(N,G,GO,GN)
% CALCULATE THE VELOCITY OF POINT DO
V2PTS(N,D,CO,DO)
% CALCULATE THE VELOCITY OF POINT FO
V2PTS(N,F,GO,FO)
