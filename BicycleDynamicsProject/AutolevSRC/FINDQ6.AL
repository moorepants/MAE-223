% JASON MOORE -- APRIL 24, 2006
% PROGRAM TO FIND AN EXPRESSION FOR THE DEPENDENT GENERALIZED COORDINATE (Q6 - FRAME PITCH) OF A FOUR BODY BICYCLE MODEL
OVERWRITE ON
AUTORHS ON
% DECLARE THE INERTIAL REFERENCE FRAME
NEWTONIAN N
% DECLARE THREE INTERMEDIATE FRAMES
FRAMES A,B,E
% DECLARE FOUR BODIES: REAR WHEEL,FRAME,FORK,FRONT WHEEL
BODIES C,D,F,G
% DECLARE FOUR POINTS: FRONT CONTACT POINT,REAR CONTACT POINT (ONE ON GROUND AND ONE ON WHEEL)
POINTS NC,CN,NG,GN
% DECLARE THE FRAME DIMENSIONS
CONSTANTS RF,RR,L{7},THETA
% DECLARE THE EIGHT GENERALIZED COORDINATES
VARIABLES Q{8}''
VARIABLES U4',U7'

% FRAME YAW
SIMPROT(N,A,3,Q3)
% FRAME ROLL
SIMPROT(A,B,-1,Q4)
% REAR WHEEL ROTATION
SIMPROT(B,C,2,Q5)
% FRAME PITCH
SIMPROT(B,D,-2,Q6)
% HEAD TUBE ANGLE
SIMPROT(D,E,-2,THETA) 
% STEERING ANGLE
SIMPROT(E,F,3,Q7)
% FRONT WHEEL ROTATION
SIMPROT(F,G,2,Q8)

% CALCULATE THE UNIT VECTOR ALONG THE LINE FROM GO TO GN
RFHAT>=UNITVEC(DOT(F2>,N3>)*F2>-N3>)
% CHECK IF RHAT> IS IN FACT PERPENDICULAR TO F2>
CHECK=DOT(RFHAT>,F2>)

% BUILD THE POSITION VECTOR FROM THE INERTIAL ORIGIN TO NC
P_NO_NC>=Q1*N1>+Q2*N2>
% BUILD THE POSITION VECTOR FROM NC TO CN
P_NC_CN>=0>
% BUILD THE POSITION VECTOR FROM CN TO CO
P_CN_CO>=RR*B3>
% BUILD THE POSITION VECTOR FROM CO TO GO
P_CO_GO>=L1*D1>-L2*F3>+L3*F1>
% BUILD THE POSITION VECTOR FROM GO TO GN
P_GO_GN>=RF*RFHAT>
% BUILD THE POSITION VECTOR FROM GN TO NG
P_GN_NG>=0>

% FIND THE N3> COMPONENT OF P_NC_NG> WHICH MUST EQUAL ZERO
ZERO=DOT(P_NC_NG>,N3>)
% LINEARIZE THE RIGHT HAND SIDE WRT Q6
ZEROL=TAYLOR(ZERO,0:1,Q6=0)
% SOLVE FOR Q6
SOLVE([ZEROL],Q6)
Q6'=DT(Q6)
Q6'=REPLACE(Q6',Q4'=U4,Q7'=U7)
Q6''=DT(Q6')
Q6''=REPLACE(Q6'',Q4'=U4,Q7'=U7,Q4''=U4',Q7''=U7')

% SAVE OUTPUT
SAVE FINDQ6.ALL